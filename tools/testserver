#!/usr/bin/env coffee

http    = require "http"
{parse} = require "url"
fs      = require "fs"
fs.path = require "path"

PORT = 7357
ROOT = fs.path.resolve __dirname, "../build/"

handler = (request, response) ->
  data = ""
  url = parse request.url, true
  try
    if responders[url.pathname]?
      responders[url.pathname] request, response, url
    else
      stats = fs.lstatSync fs.path.join ROOT, url.pathname
      if stats.isDirectory()
        index url.pathname, response
      else if stats.isFile()
        send response, url.pathname, mimetype(url.pathname)
      else
        throw "Unreadable path"
  catch error
    fail response, error
  

index = (path, response) ->
  abspath = "#{ROOT}#{path}"
  fs.readdir abspath, (err, list) ->
    if err?
      fail response, err
      return
    if list.indexOf("index.html") isnt -1
      send response, "#{path}/index.html"
    else
      links = ("""<a href="#{path}/#{file}">#{file}</a>""" for file in list when file.match /^[^.]/)
      response.writeHead 200, "Content-Type": "text/html; charset=utf-8"
      response.write """<!DOCTYPE html><html><head><title>#{path}</title></head><body>#{links.join "<br>"}</body></html>"""
      response.end()

responders =
  "/DodpMobile/Service.svc": (request, response, url) ->
    data = ""
    request.on "data", (chunk) -> data += chunk.toString "utf8"
    request.on "end", ->
      variant = if request.headers["x-testvariant"]? then ".#{request.headers["x-testvariant"]}" else ""
      action = request.headers.soapaction
      if action
        console.log "> Service: #{action} #{request.headers["x-testvariant"]}"
        console.log "< 200 Sending test/fixtures/service/#{action}#{variant}.xml"
        send response, "test/fixtures/service/#{action}#{variant}.xml", "text/xml; charset=utf-8"
      else
        fail response, "Couldn't parse request"
  
  "/DodpDistributor/Distribute.aspx": (request, response, url) ->
    if not url.query.file?
      fail response, "No file specified"
    else
      console.log "> Content: #{fs.path.basename url.query.file}"
      path = "test/fixtures/content/#{fs.path.basename url.query.file}"
      contentType = if path.match /\.x?html?$/ then "application/xhtml+xml; charset=utf-8" else null
      console.log "< 200 Sending #{path} "
      send response, path, contentType


send = (response, path, contentType = null) ->
  contentType or= mimetype path
  stream = fs.createReadStream fs.path.join(ROOT, path)
  stream.on "open", (data) ->
    response.writeHead 200, "Content-Type": contentType
  stream.on "error", (exception) ->
    fail response, exception
  stream.pipe response


fail = (response, message = "") ->
  console.error "< 500 #{message}"
  response.writeHead 500, "Content-Type": "text/plain"
  response.end "500\n\n#{message}"


mimetype = (path) ->
  switch path.split(/\.+/).pop()
    when "js"          then contentType = "text/javascript"
    when "smil", "xml" then contentType = "application/xml"
    when "xhtml"       then contentType = "application/xhtml+xml"
    else                    contentType = "text/html"
  "#{contentType}; charset=utf-8"


http.createServer(handler).listen PORT, ->
  console.log "Server running on 127.0.0.1:#{PORT}"
  console.log "Press ctrl-C to stop server"
  # require("child_process").exec "which open && open http://127.0.0.1:#{PORT}/test/index.html"
